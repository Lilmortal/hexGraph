plugins {
    id 'idea'
    id 'java'
    id "com.github.johnrengelman.shadow" version "2.0.1"
}

group 'hexGraph'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    akkaVersion = '2.5.6'
    kafkaVersion = '1.0.0'
    log4jVersion = '2.10.0'
    jacksonVersion = '2.9.3'
    gsonVersion = '2.8.2'

    jUnitVersion = '4.12'
}

dependencies {
    implementation "com.typesafe.akka:akka-actor_2.12:${akkaVersion}"
    implementation "org.apache.kafka:kafka_2.12:${kafkaVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"

    testImplementation "junit:junit:${jUnitVersion}"
}

String mainClass = 'nz.co.hexgraph.HexGraphApplication'

jar {
    manifest {
        attributes 'Main-Class': mainClass
    }
}

shadowJar {
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer) {
        resource = 'reference.conf'
    }
}

task run(type: JavaExec) {
    dependsOn shadowJar
    classpath = shadowJar.outputs.files
    main = mainClass
}